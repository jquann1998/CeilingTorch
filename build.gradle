plugins {
	id 'java-library'
	id 'eclipse'
	id 'idea'
	id 'net.neoforged.gradle.userdev' version '[7.0,7.1)'
}

version = '1.27'
group = 'bl4ckscor3.mod.ceilingtorch'

base {
	archivesName = 'ceilingtorch-1.20.4'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
	configureEach {
		systemProperty 'forge.logging.console.level', 'debug'
		modSource project.sourceSets.main
	}

	client {
		workingDirectory project.file('run/client')
	}

	server {
		workingDirectory project.file('run/server')
		programArgument '-nogui'
	}
}

repositories {
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
	implementation 'net.neoforged:neoforge:20.4.232'
	implementation 'curse.maven:jei-238222:5101381'

	implementation "curse.maven:athena-841890:5176890" //chipped dependency
	implementation "curse.maven:architectury-api-419699:5173990" //bamboo everything dependency
	implementation "curse.maven:adorn-for-forge-438784:5189507"
	implementation "curse.maven:bagus-lib-866533:5269655" //tofucraftreload dependency
	implementation "curse.maven:bamboo-everything-331723:5014705"
	implementation "curse.maven:chipped-456956:5270091"
	implementation "curse.maven:curios-309927:5248370" //occultism dependency
	implementation "curse.maven:geckolib-388172:5188406" //occultism dependency
	implementation "curse.maven:modonomicon-538392:5267193" //occultism dependency
	implementation "curse.maven:occultism-361026:5254027"
	implementation "curse.maven:reliquary-reincarnations-241319:5253732"
	implementation "curse.maven:resourceful-lib-570073:5180841" //chipped dependency
	implementation "curse.maven:sbm-bone-torch-238695:4949126"
	implementation "curse.maven:smartbrainlib-661293:5273515" //occultism dependency
	implementation "curse.maven:tofucraftreload-317469:5272782"
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title": "Ceiling Torch",
                    "Specification-Vendor": "bl4ckscor3",
                    "Specification-Version": "${version}",
                    "Implementation-Title": "Ceiling Torch",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor": "bl4ckscor3"])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}